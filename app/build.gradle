apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.uwetrottmann.wpdisplay"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 17
        versionName "13"

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        // for CI server (reports are not public)
        textReport true
        textOutput 'stdout'
    }

    signingConfigs {
        release {
            if (rootProject.file('keystore.properties').exists()) {
                def props = new Properties()
                props.load(new FileInputStream(rootProject.file('keystore.properties')))

                storeFile = file(props['storeFile'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (rootProject.file('keystore.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    // https://developer.android.com/topic/libraries/view-binding
    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // https://developer.android.com/jetpack/androidx/versions
    implementation "androidx.fragment:fragment-ktx:1.2.2"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.preference:preference-ktx:1.1.0"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    // ViewModel and LiveData
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Material Design
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
}
